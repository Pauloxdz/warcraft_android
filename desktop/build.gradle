/*
 * Blair Butterworth (c) 2019
 *
 * This work is licensed under the MIT License. To view a copy of this
 * license, visit
 *
 *      https://opensource.org/licenses/MIT
 */

plugins {
    id "java"
    id "edu.sc.seis.macAppBundle" version "2.3.0"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

sourceSets{
    main.java.srcDirs = [ "src/" ]
    test.java.srcDirs = [ "test/" ]
}

ext{
    appName = "Warcraft2"
    assetsDir = "../android/assets"
    iconPath = "../android/assets/data/icons/WarcraftIcon.icns"
    mainClass = "com.evilbird.warcraft.desktop.DesktopLauncher"
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClass
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    setIgnoreExitValue(true)
}

shadowJar {
    baseName = project.appName
    from files((String)project.assetsDir)
    exclude "*.dll"
    exclude "*.so"
    manifest {
        attributes 'Main-Class': project.mainClass
    }
    minimize()
}

macAppBundle {
    icon = project.iconPath
    appName = project.appName
    mainClassName = project.mainClass
    bundleJRE = true
    jarTask = "shadowJar"
}